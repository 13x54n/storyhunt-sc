// SPDX-License-Identifier: GPL-2.0-or-later
pragma solidity >=0.7.5;

// import '../interfaces/IERC20.sol';

import '../core/contracts/libraries/LowGasSafeMath.sol';

import '../interfaces/IPeripheryPaymentsWithFee.sol';
import './PeripheryPayments.sol';

import '../interfaces/external/IWIP9.sol';
import '../libraries/TransferHelper.sol';

abstract contract PeripheryPaymentsWithFee is PeripheryPayments, IPeripheryPaymentsWithFee {
    using LowGasSafeMath for uint256;

    /// @inheritdoc IPeripheryPaymentsWithFee
    function unwrapWIP9WithFee(
        uint256 amountMinimum,
        address recipient,
        uint256 feeBips,
        address feeRecipient
    ) public payable override {
        require(feeBips > 0 && feeBips <= 100);

        uint256 balanceWIP9 = IWIP9(WIP9).balanceOf(address(this));
        require(balanceWIP9 >= amountMinimum, 'Insufficient WIP9');

        if (balanceWIP9 > 0) {
            IWIP9(WIP9).withdraw(balanceWIP9);
            uint256 feeAmount = balanceWIP9.mul(feeBips) / 10_000;
            if (feeAmount > 0) TransferHelper.safeTransferETH(feeRecipient, feeAmount);
            TransferHelper.safeTransferETH(recipient, balanceWIP9 - feeAmount);
        }
    }

    /// @inheritdoc IPeripheryPaymentsWithFee
    function sweepTokenWithFee(
        address token,
        uint256 amountMinimum,
        address recipient,
        uint256 feeBips,
        address feeRecipient
    ) public payable override {
        require(feeBips > 0 && feeBips <= 100);

        uint256 balanceToken = IERC20(token).balanceOf(address(this));
        require(balanceToken >= amountMinimum, 'Insufficient token');

        if (balanceToken > 0) {
            uint256 feeAmount = balanceToken.mul(feeBips) / 10_000;
            if (feeAmount > 0) TransferHelper.safeTransfer(token, feeRecipient, feeAmount);
            TransferHelper.safeTransfer(token, recipient, balanceToken - feeAmount);
        }
    }
}
